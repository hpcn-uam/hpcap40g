#!/usr/bin/gnuplot
# Expects:
# -fin: Input file
# -fout: Output file

set term pngcairo size 1600,1000 dashed

maxrate = 40 * 1000
ifg = 20

set title 'HPCAP Benchmark'

set xlabel "Frame size (bytes)"
set ylabel "%"
set y2label "Mbps"

set y2tics
set ytics nomirror

set xtics 0,100,1500
set ytics
set mxtics 5
set mytics

set key bottom right

set style line 102 lc rgb '#808080' lt 3 lw 1
set style line 103 lc rgb '#A0A0A0' lt 3 lw 0.5

set grid xtics mxtics ls 102, ls 103
set grid ytics mytics ls 102, ls 103

set yrange [0:105]
set y2range [0:maxrate]

set output fout

plot \
	fin using 2:10 with linespoints ls 1 title 'Lost \%', \
	fin using 2:12 with linespoints ls 2 title 'Port Drop \%',  \
	fin using 2:4 with linespoints ls 3 title 'Send rate' axes x1y2, \
	fin using 2:(8 * $6 * $2 / ($3 * 1000000)) with linespoints ls 4 axes x1y2 title 'Capture rate', \
	maxrate*x/(x+ifg) with lines ls 5 title 'Theoretical max rate' axes x1y2
